(set vernum (getversion "libs:version.library"))
(set ver (/ vernum 65536))
(set rev (- vernum (* ver 65536)))
(set is_update 0)
(complete 0)

(set @execute-dir (pathonly @icon))
(makeassign "PLATOINST" @execute-dir)
(set term-dest (getassign "PLATOTERM" "a"))

(if term-dest
   (
      (set @default-dest term-dest)
      (set is_update 
         (askbool
            (prompt 
               "Your current copy of PLATOTerm appears to be located in the drawer named \""
               term-dest
               "\".\n\n  Do you want the update installed in that drawer?"
            )
            (help
               "The logical assignment \"PLATOTERM\" is present on your system"
               "If you would like this version placed elsewhere, please select"
               "NO.  Otherwise, select YES to install PLATOTerm there."
            )
            (default 1)
         )
      )
   )
)

(if (= is_update 0)
   (
      (set term-dest
         (tackon 
            (askdir
               (prompt "In which disk or drawer should PLATOTerm be installed?")
               (help @askdir-help)
               (default @default-dest)
            )
            "PLATOTerm"
         )
      )
      (makedir term-dest (infos))
      (makeassign "PLATOTERM" term-dest)
   )
)

(complete 25)

(copyfiles
   (source "Fonts")
   (dest "Fonts:")
   (all)
)

(copyfiles
   (source "PLATOTerm")
   (dest term-dest)
   (infos)
)

(complete 50)

(copyfiles
   (source "Read Me")
   (dest term-dest)
   (infos)
)

(copyfiles
   (source "GPL-3.0")
   (dest term-dest)
   (infos)
)

(complete 75)

(startup "PLATOTerm"
   (prompt
      "Ready to modify \"S:User-Startup\" so that PLATOTerm can be "
      "used with your system."
   )
   (help @startup-help)
   (command 
      "Assign PLATOTERM: " term-dest "\n"
      "Path PLATOTERM: add\n"
   )
)

(complete 100)

(exit)
